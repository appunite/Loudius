plugins {
    id 'com.android.application'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.dagger.hilt.android'
    id 'org.jlleitschuh.gradle.ktlint' version '11.2.0'
    id 'jacoco'
}

android {
    namespace 'com.appunite.loudius'
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.appunite.loudius"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.appunite.loudius.util.HiltTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        if (System.env.CLIENT_SECRET == null || System.env.CLIENT_SECRET.isEmpty()) {
            logger.warn("You need to set CLIENT_SECRET in your environment variables")
        }
        buildConfigField "String", "CLIENT_SECRET", "\"${System.env.CLIENT_SECRET}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug{
            enableUnitTestCoverage true
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
            excludes += '/META-INF/LICENSE.md'
            excludes += '/META-INF/LICENSE-notice.md'
        }
    }
    testOptions {
        packagingOptions {
            jniLibs {
                useLegacyPackaging true
            }
        }
    }
    kapt {
        arguments {
            arg("skipPrivatePreviews", "true")
        }
    }
}

dependencies {
    implementation project(':components')
    //Desugaring for use of java.time in api lower then 26
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.2'

    //Base android deps
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.6.1'

    //Compose
    def composeBom = platform('androidx.compose:compose-bom:2023.01.00')
    implementation composeBom
    androidTestImplementation composeBom
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material:1.4.0'
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation "androidx.navigation:navigation-compose:2.5.3"
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    //Showkase
    implementation "com.airbnb.android:showkase:1.0.0-beta18"
    kapt "com.airbnb.android:showkase-processor:1.0.0-beta18"

    //DI - Hilt
    implementation "com.google.dagger:hilt-android:2.45"
    kapt "com.google.dagger:hilt-compiler:2.45"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    //DI - for local unit tests
    testImplementation 'com.google.dagger:hilt-android-testing:2.45'
    kaptTest 'com.google.dagger:hilt-compiler:2.45'

    // DI - For instrumented tests.
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.45'
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.45'

    //coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"

    // ktor
    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-okhttp:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-gson:$ktor_version"
    testImplementation("io.ktor:ktor-client-mock:$ktor_version")

    //retrofit & okhttp
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'

    //gson
    implementation 'com.google.code.gson:gson:2.10.1'

    // assertion library
    // cannot use 0.34.0 due to an existing bug
    // https://github.com/robfletcher/strikt/issues/259
    testImplementation 'io.strikt:strikt-core:0.33.0'
    testImplementation 'io.strikt:strikt-mockk:0.33.0'
    androidTestImplementation 'io.strikt:strikt-core:0.33.0'
    androidTestImplementation 'io.strikt:strikt-mockk:0.33.0'

    //testing
    testImplementation "io.mockk:mockk:1.13.3"
    testImplementation("com.squareup.okhttp3:mockwebserver:4.10.0")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation("androidx.compose.ui:ui-test-junit4:$compose_version")
    androidTestImplementation("com.squareup.okhttp3:mockwebserver:4.11.0")
    androidTestImplementation("io.mockk:mockk-android:1.13.3")

    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.5.1'

    // Firebase instrumentation lib
    androidTestImplementation 'com.google.firebase:testlab-instr-lib:0.2'

    // ktlint
    ktlintRuleset project(":custom-ktlint-rules")
}

tasks.register('codeCoverage',JacocoReport) {
    dependsOn 'testDebugUnitTest', 'createDebugCoverageReport'
    reports {
        html.required.set(true)
        html.outputLocation =layout.buildDirectory.dir('reports/jacoco')
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ]))
}


tasks.withType(Test) {
    useJUnitPlatform()
}

kapt {
    correctErrorTypes true
}
