apply plugin: 'jacoco'

jacoco {
    toolVersion '0.8.8'
}

tasks.register('codeCoverage', JacocoReport) {

    reports {
        html.required.set(true)
        xml.required.set(true)
    }

    classDirectories.setFrom(
            fileTree(project.buildDir) {
                include("**/tmp/kotlin-classes/debug/**")
                exclude(
                        '**/R.class',
                        '**/R$*.class',
                        '**/BuildConfig.*',
                        '**/Manifest*.*',
                        '**/airbnb/**/*.*',
                        "**/*Screen*",
                        "**/Showkase*",
                        "**/theme/Theme*",
                        "**/theme/Type*",
                )
            }
    )

    sourceDirectories.setFrom(
            fileTree(dir: project.projectDir, includes: ["src/main/java/**", "src/main/kotlin/**"])
    )

    executionData.setFrom(
            fileTree(dir: "${buildDir}/test-results/instrumentation_results", includes: ['*.ec']),
            fileTree(dir: "${buildDir}/outputs/unit_test_code_coverage", includes: ["**/*.exec", "**/*.ec"])
    )
}
